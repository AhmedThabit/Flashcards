apply plugin: 'java'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: "net.ltgt.apt"
apply plugin: 'jacoco'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def sdkDir = properties.getProperty('sdk.dir')

repositories {
    maven { url "$sdkDir/extras/android/m2repository" }
}

retrolambda {
    oldJdk System.getenv("JAVA7_HOME")
}

jacoco {
    toolVersion = "0.7.6.201602180812"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, excludes: [
                    '**/*AutoValue*.class', // AutoValue generated classes
                    '**/*Lambda*',  // Generated lambdas
                    '**/*_Factory*', // Dagger2 generated factories
                    '**/*Module_*Factory*' // Dagger2 generated factories
            ])
        })
    }
}

apply from: '../dependencies.gradle'

dependencies {

    compile 'com.google.code.gson:gson:2.4'
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    compile 'com.squareup.retrofit2:retrofit-mock:2.0.2'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.2'

    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.2.0'

    compile 'com.facebook.stetho:stetho-okhttp3:1.3.1'

    compile 'io.reactivex:rxjava:1.1.5'
    compile 'com.annimon:stream:1.0.9'

    compile dep.SUPPORT_ANNOTATION
    compile 'org.slf4j:slf4j-api:1.7.21'


    compile "com.google.auto.value:auto-value:1.2"
    apt "com.google.auto.value:auto-value:1.2"
    apt 'com.gabrielittner.auto.value:auto-value-with:1.0.0-rc1'
    compile 'com.google.guava:guava:19.0'

    compile dep.DAGGER
    apt dep.DAGGER_COMPILER

    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.slf4j:slf4j-simple:1.7.21'

}
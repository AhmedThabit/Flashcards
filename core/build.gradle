apply plugin: 'java'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'net.ltgt.apt'
apply plugin: 'jacoco'
apply plugin: 'idea'
apply from: '../dependencies.gradle'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def sdkDir = properties.getProperty('sdk.dir')

repositories {
    maven { url "$sdkDir/extras/android/m2repository" }
}

retrolambda {
    oldJdk System.getenv("JAVA7_HOME")
}

jacoco {
    toolVersion = "0.7.6.201602180812"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, excludes: [
                    '**/*AutoValue*.class', // AutoValue generated classes
                    '**/*Lambda*',  // Generated lambdas
                    '**/*_Factory*', // Dagger2 generated factories
                    '**/*Module_*Factory*' // Dagger2 generated factories
            ])
        })
    }
}

check.dependsOn jacocoTestReport

dependencies {

    compile "io.reactivex.rxjava2:rxjava:2.0.6"

    compile 'com.google.code.gson:gson:2.4'
    compile 'com.google.code.findbugs:jsr305:3.0.1'

    compile "com.annimon:stream:$STREAM_VERSION"
    compile "com.android.support:support-annotations:$SUPPORT_VERSION"

    // use api as provided-only, we don't need it in jar
    compileOnly "org.slf4j:slf4j-api:$SLF4J_VERSION"

    // Only annotations are used
    compile "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    compile "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"

    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    apt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"

    compile "com.google.auto.value:auto-value:$AUTOVALUE_VERSION"
    apt "com.google.auto.value:auto-value:$AUTOVALUE_VERSION"
    apt 'com.gabrielittner.auto.value:auto-value-with:1.0.0-rc1'

    testCompile "org.mockito:mockito-core:$MOCKITO_VERSION"
    testCompile "junit:junit:$JUNIT_VERSION"

}
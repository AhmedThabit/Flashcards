apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.squareup.sqldelight'


android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "com.triangleleft.flashcards"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "REST_SERVICE_URL", "\"https://www.duolingo.com\""
        buildConfigField "String", "TRANSLATION_SERVICE_URL", "\"https://d2.duolingo.com\""
        testInstrumentationRunner "com.triangleleft.flashcards.MockTestRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testCoverageEnabled true
        }
        debug {
            testCoverageEnabled true
        }
        appium {
            initWith debug
            buildConfigField "String", "REST_SERVICE_URL", "\"http://10.0.2.2:8080\""
            buildConfigField "String", "TRANSLATION_SERVICE_URL", "\"http://10.0.2.2:8080\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false // ignoring some references from dagger-compiler
    }
    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res'
            ]
        }
    }
}


apply from: '../dependencies.gradle'

dependencies {
    compile project(":core")


    compile "com.android.support:appcompat-v7:$SUPPORT_VERSION"
    compile "com.android.support:design:$SUPPORT_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_VERSION"
    compile "com.android.support:cardview-v7:$SUPPORT_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_VERSION"
    compile "com.android.support:preference-v14:$SUPPORT_VERSION"
    compile "com.android.support:support-annotations:$SUPPORT_VERSION"

    // We have to exclude timber here, as this lib is jar, and timber is aar, and gradle doesn't
    // allow jar libs to depend on aars: (Error:Module <> depends on one or more Android Libraries but is a jar)
    compile('com.arcao:slf4j-timber:2.1') {
        exclude group: 'com.jakewharton.timber', module: 'timber'
    }
    compile "org.slf4j:slf4j-api:1.7.21"
    compile 'com.jakewharton.timber:timber:4.1.2'

    compile "com.github.franmontiel:PersistentCookieJar:$PCJ_VERSION"
    compile "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:retrofit-mock:$RETROFIT_VERSION"

    // We have to exclude all rx dependencies as we are using custom one
    // though probably we shouldn't use jar lib and use jitpack instead
    compile("com.squareup.retrofit2:adapter-rxjava:$RETROFIT_VERSION") {
        exclude group: 'io.reactivex', module: 'rxjava'
    }
    compile('io.reactivex:rxandroid:1.2.0') {
        exclude group: 'io.reactivex', module: 'rxjava'
    }
    compile "com.squareup.okhttp3:logging-interceptor:$OKHTTP_VERSION"
    compile "com.facebook.stetho:stetho-okhttp3:$STETHO_VERSION"
    compile "com.facebook.stetho:stetho:$STETHO_VERSION"

    //compile 'com.daprlabs.aaron:cardstack:0.3.0'
    compile('com.github.lekz112:SwipeDeck2:preview_layout-SNAPSHOT') {
        //compile('com.daprlabs.aaron:swipedeck:2.0.5') {
        exclude group: 'io.reactivex', module: 'rxjava'
        exclude group: 'io.reactivex', module: 'rxandroid'
    }
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.triangleleft.assertdialog:assert-dialog:0.8.5'
    compile 'com.yqritc:recyclerview-flexibledivider:1.2.5'
    compile 'javax.inject:javax.inject:1'
    compile 'javax.annotation:javax.annotation-api:1.2'
    compile 'com.makeramen:roundedimageview:2.2.1'
    compile 'com.github.AndroidDeveloperLB:AutoFitTextView:3'
    compile 'com.pnikosis:materialish-progress:1.7'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'de.hdodenhof:circleimageview:2.0.0'
    compile 'com.github.romainguy:ViewServer:017c01cd51'

    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    apt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    // Needed for annotations
    provided "com.google.auto.value:auto-value:$AUTOVALUE_VERSION"
    apt "com.google.auto.value:auto-value:$AUTOVALUE_VERSION"

    // For some reason excluding support-annotations from runner doesn't work, so just add it directly
    androidTestCompile "com.android.support:support-annotations:$SUPPORT_VERSION"
    androidTestCompile "org.mockito:mockito-core:$MOCKITO_VERSION"
    androidTestCompile "com.crittercism.dexmaker:dexmaker:$DEXMAKER_VERSION"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-dx:$DEXMAKER_VERSION"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-mockito:$DEXMAKER_VERSION"
    androidTestCompile "junit:junit:$JUNIT_VERSION"
    androidTestCompile('com.android.support.test:runner:0.5') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-intents:2.2.2') {
        exclude group: 'com.android.support.test.espresso', module: 'espresso-core'
    }
    androidTestApt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"

    testCompile "junit:junit:$JUNIT_VERSION"
    testCompile "org.mockito:mockito-core:$MOCKITO_VERSION"
}
